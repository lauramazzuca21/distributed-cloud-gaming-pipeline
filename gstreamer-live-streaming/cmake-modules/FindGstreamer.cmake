FIND_PACKAGE(PkgConfig REQUIRED)
# use pkg-config to get the directories and then use these values
# in the FIND_PATH() and FIND_LIBRARY() calls
# don't make this check required - otherwise you can't use macro_optional_find_package on this one
PKG_CHECK_MODULES(PKG_GSTREAMER gstreamer-1.0)
SET(GSTREAMER_VERSION ${PKG_GSTREAMER_VERSION})
SET(GSTREAMER_DEFINITIONS ${PKG_GSTREAMER_CFLAGS})

FIND_PATH(GSTREAMER_INCLUDE_DIR gst/gst.h
  PATHS
  ${PKG_GSTREAMER_INCLUDE_DIRS}
  PATH_SUFFIXES gstreamer-1.0
)

IF(NOT GSTREAMER_INCLUDE_DIR)
  MESSAGE(STATUS "GStreamer: WARNING: include dir not found")
ENDIF(NOT GSTREAMER_INCLUDE_DIR)

FIND_PATH(GSTREAMER_WEBRTC_INCLUDE_DIR gst/webrtc/webrtc_fwd.h
  PATHS
  ${PKG_GSTREAMER_INCLUDE_DIRS}
  PATH_SUFFIXES gstreamer-1.0
)

# Newer versions of GStreamer put gst-config.h under lib/gstreamer-1.0/include
FIND_PATH(GSTREAMER_CONF_INCLUDE_DIR gst/gstconfig.h
  PATHS
  ${PKG_GSTREAMER_INCLUDE_DIRS}
  PATH_SUFFIXES gstreamer-1.0 gstreamer-1.0/include
)

IF(NOT GSTREAMER_CONF_INCLUDE_DIR)
  MESSAGE(STATUS "GStreamer: WARNING: gstconfig.h not found")
ENDIF(NOT GSTREAMER_CONF_INCLUDE_DIR)

IF(GSTREAMER_INCLUDE_DIR AND GSTREAMER_CONF_INCLUDE_DIR)
  IF(NOT GSTREAMER_CONF_INCLUDE_DIR STREQUAL GSTREAMER_INCLUDE_DIR)
    LIST(APPEND GSTREAMER_INCLUDE_DIR "${GSTREAMER_CONF_INCLUDE_DIR}")
  ENDIF()
ENDIF(GSTREAMER_INCLUDE_DIR AND GSTREAMER_CONF_INCLUDE_DIR)

FIND_LIBRARY(GSTREAMER_LIBRARIES NAMES gstreamer-1.0
  PATHS ${PKG_GSTREAMER_LIBRARY_DIRS}
)

FIND_LIBRARY(GSTREAMER_WEBRTC_LIBRARY NAMES gstwebrtc-1.0
  PATHS ${PKG_GSTREAMER_LIBRARY_DIRS}
)

FIND_LIBRARY(GSTREAMER_SDP_LIBRARY NAMES gstsdp-1.0
  PATHS ${PKG_GSTREAMER_LIBRARY_DIRS}
)

IF(GSTREAMER_LIBRARIES)
ELSE(GSTREAMER_LIBRARIES)
  MESSAGE(STATUS "GStreamer: WARNING: library not found")
ENDIF(GSTREAMER_LIBRARIES)

IF(GSTREAMER_WEBRTC_INCLUDE_DIR AND GSTREAMER_WEBRTC_LIBRARY AND GSTREAMER_SDP_LIBRARY)
  SET(GSTREAMER_WEBRTC_FOUND TRUE)
ELSE(GSTREAMER_WEBRTC_INCLUDE_DIR AND GSTREAMER_WEBRTC_LIBRARY AND GSTREAMER_SDP_LIBRARY)
  SET(GSTREAMER_WEBRTC_FOUND FALSE)
ENDIF(GSTREAMER_WEBRTC_INCLUDE_DIR AND GSTREAMER_WEBRTC_LIBRARY AND GSTREAMER_SDP_LIBRARY)

IF(GSTREAMER_VERSION)
  find_package_handle_standard_args(Gstreamer
    REQUIRED_VARS
      GSTREAMER_LIBRARIES
      GSTREAMER_INCLUDE_DIR
    VERSION_VAR
      GSTREAMER_VERSION
    FAIL_MESSAGE
      "Could NOT find Gstreamer, try to set the path to Gstreamer root folder in the system variable"
  )
ELSE()
  find_package_handle_standard_args(Gstreamer "Could NOT find Gstreamer, try to set the path to Gstreamer root folder in the system variable"
    GSTREAMER_LIBRARIES
    GSTREAMER_INCLUDE_DIR
  )
ENDIF()

MARK_AS_ADVANCED(GSTREAMER_INCLUDE_DIR GSTREAMER_WEBRTC_INCLUDE_DIR GSTREAMER_LIBRARIES GSTREAMER_WEBRTC_LIBRARY GSTREAMER_SDP_LIBRARY)

